000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.     XXXP12DK.
000400 AUTHOR.         NAME.
000500 DATE-WRITTEN.   APRIL, 1600.
005400*************************************************************
005500 SKIP2
005600 ENVIRONMENT DIVISION.
005700 INPUT-OUTPUT SECTION.
005800 FILE-CONTROL.
005900 SKIP1
006000     SELECT ERROR-FILE             ASSIGN UT-S-ERRORS.
006100 SKIP1
006200     SELECT XXXP-AUDIT-STRIP       ASSIGN UT-S-A20F001A.
006300 SKIP1
006400     SELECT XXXP-DAILY-AUDIT       ASSIGN UT-S-A20F001B.
006500 SKIP2
006600 DATA DIVISION.
006700 FILE SECTION.
006800 SKIP1
006900 FD  ERROR-FILE
007000     LABEL RECORDS ARE OMITTED.
007100 01  ERROR-REC                   PIC X(133).
007200 SKIP1
007300 FD  XXXP-AUDIT-STRIP
007400     BLOCK CONTAINS 0 RECORDS
007500     LABEL RECORDS ARE OMITTED.
007600 01  XXXP-AUDIT-RECORD           PIC X(150).
007700 SKIP1
007800 FD  XXXP-DAILY-AUDIT
007900     BLOCK CONTAINS 0 RECORDS
008000     LABEL RECORDS ARE OMITTED.
008100 01  XXXP-DISTRICT-RECORD        PIC X(276).
008200 EJECT
008300 WORKING-STORAGE SECTION.
008400 SKIP1
008500 77  W-S                         PIC X(24)
008600     VALUE 'XXXP12DK-WORKING STORAGE'.
008700 77  SUB-DIV                     PIC 9(4)   COMP SYNC.
008800 77  MAX-SUB-DIV                 PIC 9(4)   COMP SYNC VALUE 20.
008900 77  RECORD-COUNT                PIC S9(5) COMP-3     VALUE +0.
009000 SKIP1
009100 01  INVALID-NAME-MESSAGES.
009200     05  INVALID-DEPT-MSG        PIC X(20)   VALUE
009300         'INVALID DEPARTMENT  '.
009400     05  INVALID-DIST-MSG        PIC X(20)   VALUE
009500         'INVALID DISTRICT    '.
009600     05  INVALID-DIV-MSG         PIC X(20)   VALUE
009700         'INVALID DIVISION    '.
009800     SKIP1
009900 01  WS-FIELDS.
010000     05  CTR                     PIC S9(5) COMP VALUE ZERO.
010100     05  SEARCH-FLAG             PIC X       VALUE SPACE.
010200         88  DONE-SEARCHING                  VALUE 'Y'.
010300     05  MATCH-FLAG              PIC X       VALUE SPACE.
010400         88  FOUND-MATCH                     VALUE 'Y'.
010500     05  EOF-FLAG                PIC X       VALUE SPACE.
010600         88  MORE-RECORDS                    VALUE ' '.
010700         88  NO-MORE-RECORDS                 VALUE 'Y'.
010800     05  HEAD-FLAG               PIC X       VALUE SPACE.
010900         88  FIRST-TIME                      VALUE SPACE.
011000     05  ERROR-FLAGS.
011100       10  STORE-FLAG            PIC X       VALUE SPACE.
011200         88  STORE-ERROR                     VALUE 'Y'.
011300       10  DEPT-FLAG             PIC X       VALUE SPACE.
011400         88  DEPT-ERROR                      VALUE 'Y'.
011500       10  DISTRICT-FLAG         PIC X       VALUE SPACE.
011600         88  DISTRICT-ERROR                  VALUE 'Y'.
011700       10  DIVISION-FLAG         PIC X       VALUE SPACE.
011800         88  DIVISION-ERROR                  VALUE 'Y'.
011900 SKIP1
012000 01  WS-KEEP.
012100     05  LOOK-UP-TABLES.
012200       08  DEPT-TABLE OCCURS 2000 TIMES.
012300         10  DEPT-NUMBER         PIC XXX.
012400         10  DEPT-NAME           PIC X(30).
012500       08  DISTRICT-TABLE OCCURS 99 TIMES.
012600         10  DISTRICT-NAME       PIC X(30).
012700       08  DIVISION-TABLE OCCURS 20 TIMES.
012800         10  DIVISION-NAME       PIC X(30).
012900     05  DEPT-CTR                PIC S9(3) COMP-3.
013000     05  MAX-DEPT-NBR            PIC S9(4) COMP-3 VALUE 2000.
013100 SKIP1
013200 01  SPACED-OUT                  PIC X(133)  VALUE SPACES.
013300 EJECT
013400 01  WS-STC-SCTB0000             PIC X(2)  VALUE SPACES.
013500 01  WS-CONTROL-CARD.
013600     03  WS-PROGRAM-ID           PIC X(8).
013700         88  WS-DAILY-LOOKUP     VALUE 'XXXP10DK'.
013800         88  WS-PERIOD-LOOKUP    VALUE 'XXXP60PK'.
013900     03  FILLER                  PIC X.
014000     03  WS-CHECKPOINT-CONTROL   PIC 9(4).
014100
014200 01  DEPT-KEY.
014300     05  FILLER                  PIC XXX     VALUE '200'.
014400     05  DEPT-CODE               PIC XXX     VALUE SPACE.
014500     05  FILLER                  PIC X(4)    VALUE SPACES.
014600     SKIP1
014700 01  DISTRICT-KEY.
014800     05  FILLER                  PIC XXX     VALUE '201'.
014900     05  DISTRICT-CODE           PIC 999.
015000     05  FILLER                  PIC X(4)    VALUE SPACES.
015100     SKIP1
015200 01  DIVISION-KEY.
015300     05  FILLER                  PIC XXX     VALUE '203'.
015400     05  DIVISION-CODE           PIC 99.
015500     05  FILLER                  PIC X(5)    VALUE SPACES.
015600     EJECT
015700 01  ERROR-HEAD.
015800     05  FILLER      PIC X(20) VALUE ' XXXP12DK'.
015900     05  FILLER      PIC X(32) VALUE 'ERROR MESSAGE'.
016000     05  FILLER      PIC X(22) VALUE 'CO  MAJOR  MIN  STORE'.
016100     05  FILLER      PIC X(21) VALUE 'DEPT MAJOR-ACCT-TYPE'.
016200     05  FILLER      PIC X(19) VALUE 'ACCT-TYPE DIST/DIV'.
016300     05  FILLER      PIC X(15) VALUE 'RC-DATE RC-TIME'.
016400     SKIP1
016500 01  ERROR-MESSAGES.
016600     05  E-MSG1 PIC X(58) VALUE ' ***XXXP12DK-DEPT TABLE TOO SMALL
016700-                               '. ALERT FIN PROGRAMMER***'.
016800     05  E-MSG2 PIC X(62) VALUE ' ***XXXP12DK-DISTRICT TABLE TOO S
016900-                               'MALL. ALERT FIN PROGRAMMER***'.
017000     05  E-MSG3 PIC X(62) VALUE ' ***XXXP12DK-DIVISION TABLE TOO S
017100-                               'MALL. ALERT FIN PROGRAMMER***'.
017200     SKIP1
017300 01  ERROR-OUT.
017400     05  FILLER      PIC X     VALUE SPACE.
017500     05  ERROR-MSG   PIC X(50) VALUE SPACES.
017600     05  E-COMPANY   PIC XX    VALUE SPACES.
017700     05  FILLER      PIC XX    VALUE SPACES.
017800     05  E-ACCT      PIC X(5)  VALUE SPACES.
017900     05  FILLER      PIC XX    VALUE SPACES.
018000     05  E-MIN       PIC X(3)  VALUE SPACES.
018100     05  FILLER      PIC X(2)  VALUE SPACES.
018200     05  E-STORE     PIC 9(5)  VALUE ZEROES.
018300     05  FILLER      PIC XX    VALUE SPACES.
018400     05  E-DEPT      PIC XXX   VALUE SPACES.
018500     05  FILLER      PIC X(9)  VALUE SPACES.
018600     05  E-MAJ-TYPE  PIC X     VALUE SPACES.
018700     05  FILLER      PIC X(12) VALUE SPACES.
018800     05  E-ACCT-TYPE PIC X     VALUE SPACES.
018900     05  FILLER      PIC X(7)  VALUE SPACES.
019000     05  E-DIST-DIV  PIC 999   VALUE ZEROES.
019100     05  FILLER      PIC X(4)  VALUE SPACES.
019200     05  E-DATE      PIC Z(7).
019300     05  FILLER      PIC X     VALUE SPACES.
019400     05  E-TIME      PIC Z(7).
019500     EJECT
020000 01  DA-REC-IN.
020100     05  DA-1        PIC X(50).
020200     05  DA-2        PIC X(100).
020300 01  WWWS0003-LOCATION-TRANSI PIC X(8) VALUE 'WWWS0003'.
020400 01  WWWS0040-STR-DEPT-TRANSI PIC X(8) VALUE 'WWWS0040'.
020500 01  WWWSSS00-CONTROL-SUBR    PIC X(8) VALUE 'WWWSSS00'.
020700 01  PPPTRL01-DUMMY           PIC X(413) VALUE SPACES.
020800 01  WS-ST-STORE-NUMBER       PIC 9(05) VALUE ZEROES.
020900     EJECT
021000 COPY WXXN001A.
021100************ MIGRATE TO DB2  MFR CHANGES START**************
021200 COPY TTTN2001.
021200 COPY NNNN0000.
021300 COPY YYYN005A.
021400 COPY XXXN001A.
021500 COPY WWWC0003.
021600 COPY WWWC0040.
021700 COPY WWWC0100.
021800     EXEC SQL
021900         INCLUDE SQLCA
022000     END-EXEC.
039450* ========================< DCL GENS >===========================
039460* PLACE ALL DCL GENS IN THIS SECTION.
039470* ===============================================================
039480     EXEC SQL
039490       INCLUDE DDDTFD01
039491     END-EXEC.
039450* ========================< DCL GENS >===========================
039460* PLACE ALL DCL GENS IN THIS SECTION.
039470* ===============================================================
039480     EXEC SQL
039490       INCLUDE DDDTDI01
039491     END-EXEC.
039450* ========================< DCL GENS >===========================
022100************ MIGRATE TO DB2  MFR CHANGES END****************
022200     EJECT
022300                       COPY 'XXXPSTTT' REPLACING XXXPSTTT BY
022400     STORE-I-O-AREA.
022500     EJECT
022600                       COPY 'XXXLCT20' REPLACING XXXLCT20 BY
022700     DEPT-I-O-AREA.
022800     SKIP2
022900                       COPY 'XXXLCT21' REPLACING XXXLCT21 BY
023000     DISTRICT-I-O-AREA.
023100     SKIP2
023200                       COPY 'XXXLCT23' REPLACING XXXLCT23 BY
023300     DIVISION-I-O-AREA.
023400     EJECT
024100 LINKAGE SECTION.
025000 PROCEDURE DIVISION.
025500 PROCESS-CONTROL.
025600************ MIGRATE TO DB2  MFR CHANGES START**************
025700     PERFORM CONTROL-SUBR.
025800************ MIGRATE TO DB2  MFR CHANGES END****************
025900     MOVE ZEROS TO WS-ST-STORE-NUMBER.
026000     MOVE ZEROS TO ST-STORE-NUMBER.
026100     MOVE ZERO TO DEPT-CTR.
026200     MOVE SPACES TO LOOK-UP-TABLES.
026300     MOVE SPACES TO DEPT-CODE.
026400     MOVE ZEROS TO DISTRICT-CODE, DIVISION-CODE.
026500     SKIP1
026600     OPEN INPUT  XXXP-AUDIT-STRIP
026700          OUTPUT XXXP-DAILY-AUDIT
026800                 ERROR-FILE.
026900     SKIP1
027000     ACCEPT WS-CONTROL-CARD.
027100     SKIP1
027200     PERFORM PROCESS-RECORD THRU PROCESS-RECORD-EXIT
027300         UNTIL NO-MORE-RECORDS.
027400     SKIP1
027500     PERFORM CLOSE-FILES.
027600     GOBACK.
027700     SKIP2
027800************ MIGRATE TO DB2  MFR CHANGES START**************
027900 CONTROL-SUBR.
028000     INITIALIZE XXXN001A
028100                WWWC0100
028200
028300     SET WWWC0100-SET-TASK     TO TRUE
028400     SET WWWC0100-OLD-2-NEW    TO TRUE
028500     SET WWWC0100-UPDATES-MADE TO TRUE
028600
028700     CALL WWWSSS00-CONTROL-SUBR USING
028800              XXXN001A
028900              WWWC0100.
029000************ MIGRATE TO DB2  MFR CHANGES END****************
029100 READ-STRIP.
029200     READ XXXP-AUDIT-STRIP INTO DA-REC-IN
029300         AT END  MOVE 'Y' TO EOF-FLAG.
029400     MOVE SPACES TO ERROR-FLAGS.
029500     ADD +1 TO RECORD-COUNT.
029600     SKIP2
029700 WRITE-OUTPUT-RECORD.
029800         WRITE XXXP-DISTRICT-RECORD FROM DA-DAILY-AUDIT-RECORD.
029900     EJECT
030000 PROCESS-RECORD.
030100     PERFORM READ-STRIP.
030200     IF NO-MORE-RECORDS
030300         GO TO PROCESS-RECORD-EXIT.
030400     MOVE DA-1 TO DA-LOG-01.
030500     MOVE DA-2 TO DA-LOG-10.
030600
030700     IF WS-DAILY-LOOKUP
030800         IF DA-NOT-AUDITED OR DA-NOT-REPORTED
030900             PERFORM CHECK-FOR-AUDIT-REPORT.
031000     IF WS-PERIOD-LOOKUP
031100         PERFORM CHECK-FOR-AUDIT-REPORT
031200         IF ST-XXX-STORE
031300D            DISPLAY 'STORE NUMBER ' DA-STORE-NUMBER
031400             PERFORM DIVISION-PROCESS
031500         ELSE
031600             MOVE ZEROES TO DA-DIVISION-NUMBER
031700             MOVE 'INVALID DIVISION' TO DA-DIVISION-NAME.
031800
031900     PERFORM WRITE-OUTPUT-RECORD.
032000     SKIP1
032100     IF DA-NOT-AUDITED AND DA-NOT-REPORTED
032200         IF ERROR-FLAGS NOT = SPACES
032300             PERFORM ERROR-PARA.
032400     SKIP1
032500 PROCESS-RECORD-EXIT.
032600     EXIT.
032700     SKIP2
032800 CHECK-FOR-AUDIT-REPORT.
032900     IF DA-STORE-NUMBER NUMERIC
033000        IF DA-STORE-NUMBER = ST-STORE-NUMBER
033100            MOVE ST-STORE-NAME TO DA-STORE-NAME
033200            MOVE ST-REPORTING-SEQUENCE-NO TO
033300                 DA-STORE-REPORTING-SEQ-NO
033400            MOVE ST-DISTRICT-CODE TO DA-DISTRICT-NUMBER
                  IF ST-XXX-STORE
033500               PERFORM DEPT-MATCH
                  ELSE
                     MOVE SPACES TO DA-DEPT-NAME
                  END-IF
033600        ELSE
033700            PERFORM GET-STORE
033800     ELSE
033900        MOVE SPACES TO DA-STORE-NAME
034000        MOVE SPACES TO DA-STORE-REPORTING-SEQ-NO
034100        MOVE 0      TO DA-DISTRICT-NUMBER
034200        MOVE SPACES TO DA-DEPT-NAME
034300                       DA-DISTRICT-NAME.
034400     EJECT
034500************ MIGRATE TO DB2  MFR CHANGES START**************
034600 GET-STORE.
035200     MOVE DA-STORE-NUMBER TO ST-STORE-NUMBER
035300     IF DA-STORE-NUMBER  < 9999
035400        MOVE 'S' TO ST-STORE-TYPE
035500     ELSE
035600        MOVE 'A' TO ST-STORE-TYPE
035700     END-IF
035800     INITIALIZE WWWC0003
035900     SET YYYN005A-BATCH-ENV            TO  TRUE
036000     SET WWWC0003-DDDTRL01-NOT-CURRENT TO  TRUE
036100     SET EXIT-GET-UNIQUE-ROW  TO  TRUE
036200     CALL WWWS0003-LOCATION-TRANSI USING
036300         XXXN001A
036400         YYYN005A
036500         NNNN0000-PARMS
036600         WWWC0003
036700         STORE-I-O-AREA
036800         PPPTRL01-DUMMY
037000     EVALUATE TRUE
037100     WHEN SUCCESS
037200       IF DB2-SQL-CODE = 0
037300        IF ST-XXX-STORE
037400           MOVE ST-STORE-NAME TO DA-STORE-NAME
037500           MOVE ST-REPORTING-SEQUENCE-NO TO
037600                          DA-STORE-REPORTING-SEQ-NO
037700           MOVE ST-DISTRICT-CODE TO DA-DISTRICT-NUMBER
037800           PERFORM DEPT-MATCH
037900        ELSE
038000           MOVE SPACES TO DA-STORE-NAME
038100           MOVE SPACES TO DA-STORE-REPORTING-SEQ-NO
038200           MOVE 0 TO DA-DISTRICT-NUMBER
038300           MOVE SPACES TO DA-DEPT-NAME
038400        END-IF
038500       END-IF
038600     WHEN FAILURE
038700       IF DB2-SQL-CODE = +100
038800          MOVE ZEROES TO ST-STORE-NUMBER
039000          MOVE SPACES TO DA-STORE-NAME
039100                         DA-STORE-REPORTING-SEQ-NO
039200                         DA-DEPT-NAME
039300                         DA-DISTRICT-NAME
039400          MOVE 0 TO      DA-DISTRICT-NUMBER
039500          MOVE 'Y' TO STORE-FLAG
039700       END-IF
039800     WHEN OTHER
039900             PERFORM GET-STORE-ERROR.
040000************ MIGRATE TO DB2  MFR CHANGES END****************
040100     EJECT
040200 DEPT-MATCH.
040300     MOVE 1 TO CTR.
040400     PERFORM DEPT-TABLE-SEARCH UNTIL DONE-SEARCHING.
040500     MOVE SPACE TO SEARCH-FLAG.
040600     IF FOUND-MATCH
040700         MOVE SPACE TO MATCH-FLAG
040800     ELSE
040900         PERFORM GET-DEPT.
041000     PERFORM DISTRICT-MATCH.
041100     SKIP2
041200 DEPT-TABLE-SEARCH.
041300     IF CTR GREATER THAN MAX-DEPT-NBR
041400          OR DEPT-NUMBER (CTR) = SPACES
041500         MOVE 'Y' TO SEARCH-FLAG
041600     ELSE IF DEPT-NUMBER (CTR) = DA-DEPT-SUB-DEPT
041700              MOVE 'Y' TO MATCH-FLAG  SEARCH-FLAG
041800              MOVE DEPT-NAME (CTR) TO DA-DEPT-NAME.
041900     ADD 1 TO CTR.
042000     SKIP2
042100************ MIGRATE TO DB2  MFR CHANGES START**************
042200 GET-DEPT.
042300     MOVE DA-DEPT-SUB-DEPT TO DEPT-CODE.
043000     MOVE '200' TO CT-DEPARTMENT-SEQUENCE
043100     MOVE DEPT-KEY TO DEPT-I-O-AREA(1:10)
043300     SET YYYN005A-BATCH-ENV   TO  TRUE
043400     SET EXIT-GET-UNIQUE-ROW  TO  TRUE
049400     CALL WWWS0040-STR-DEPT-TRANSI USING
049500                                  XXXN001A
049600                                  YYYN005A
049700                                  NNNN0000-PARMS
049800                                  WWWC0040
049900                                  DEPT-I-O-AREA
044100     EVALUATE TRUE
044200     WHEN SUCCESS
044300       IF DB2-SQL-CODE = 0
044400         MOVE CT-DEPARTMENT-NAME TO DA-DEPT-NAME
044500         IF DEPT-CTR < MAX-DEPT-NBR
044600            ADD 1 TO DEPT-CTR
044700            MOVE DA-DEPT-SUB-DEPT TO DEPT-NUMBER (DEPT-CTR)
044800            MOVE CT-DEPARTMENT-NAME TO DEPT-NAME (DEPT-CTR)
044900         ELSE
045000            MOVE E-MSG1 TO ERROR-REC
045100            WRITE ERROR-REC
045200            WRITE ERROR-REC FROM SPACED-OUT
045300         END-IF
046200       END-IF
046300      WHEN NOT SUCCESS
045700        IF DB2-SQL-CODE = 100
045800         MOVE SPACES           TO ST-DEPARTMENT-KEY
045900         MOVE INVALID-DEPT-MSG TO DA-DEPT-NAME
046000         MOVE 'Y' TO DEPT-FLAG
              END-IF
046300      WHEN OTHER
046400         MOVE 'GET DEPT' TO DB2-ERROR-PARAGRAPH
052720         SET DB2121-PRINT-AND-DUMP TO TRUE
052730         CALL 'TTTS2001' USING SQLCA
052740                                 DB2121-MESSAGE-AREA.
046900     EJECT
047000 DISTRICT-MATCH.
047100     IF DA-DISTRICT-NUMBER GREATER THAN 99
047200         MOVE E-MSG2 TO ERROR-REC
047300         WRITE ERROR-REC
047400         WRITE ERROR-REC FROM SPACED-OUT
047500         PERFORM GET-DISTRICT
047600     ELSE IF DISTRICT-NAME (DA-DISTRICT-NUMBER) NOT = SPACES
047700             MOVE DISTRICT-NAME (DA-DISTRICT-NUMBER)
047800             TO DA-DISTRICT-NAME
047900          ELSE PERFORM GET-DISTRICT.
048000     SKIP2
048100 GET-DISTRICT.
048300     MOVE DA-DISTRICT-NUMBER TO DISTRICT-CODE.
049000     MOVE '201' TO CT-DISTRICT-SEQUENCE
049100     MOVE DISTRICT-KEY TO DISTRICT-I-O-AREA(1:10)
           MOVE DISTRICT-CODE      TO DIST-ID OF DCLXXXT
049200     SET YYYN005A-BATCH-ENV    TO  TRUE

           EXEC SQL
             SELECT  DIST_NM
               INTO :DCLXXXT.DIST-NM
             FROM XXXT
             WHERE DIST_ID = :DCLXXXT.DIST-ID
           END-EXEC

050200     MOVE SQLCODE TO DB2-SQL-CODE
050200
050200     EVALUATE TRUE
050300     WHEN SUCCESS
050400       IF DB2-SQL-CODE = 0
               MOVE DIST-NM OF DCLXXXT  TO CT-DISTRICT-NAME             
050500         MOVE CT-DISTRICT-NAME TO DA-DISTRICT-NAME
050600         IF DA-DISTRICT-NUMBER LESS THAN 100
050700             MOVE CT-DISTRICT-NAME TO
050800               DISTRICT-NAME (DA-DISTRICT-NUMBER)
050900         ELSE NEXT SENTENCE
051000         END-IF
051200       ELSE IF DB2-SQL-CODE = 100
051300          MOVE INVALID-DIST-MSG TO DA-DISTRICT-NAME
051400          MOVE 'Y' TO DISTRICT-FLAG
051600       END-IF
051700       END-IF
051800     WHEN OTHER
051900         MOVE 'GET DIST' TO DB2-ERROR-PARAGRAPH
052720         SET DB2121-PRINT-AND-DUMP TO TRUE
052730         CALL 'TTTS2001' USING SQLCA
052740                                 DB2121-MESSAGE-AREA.
052300     EJECT
052400************ MIGRATE TO DB2  MFR CHANGES END****************
052500 DIVISION-PROCESS.
052600D    DISPLAY 'GETTING DIVISION FOR ' FN-DIVISION-CD.
052700     IF FN-DIVISION-CD GREATER THAN ZERO
052800         MOVE FN-DIVISION-CD TO SUB-DIV
052900         IF DIVISION-NAME (SUB-DIV) EQUAL SPACE
053000             PERFORM GET-DIVISION-NAME
053100         END-IF
053200         MOVE DIVISION-NAME (SUB-DIV) TO DA-DIVISION-NAME
053300         MOVE FN-DIVISION-CD TO DA-DIVISION-NUMBER.
053400D        DISPLAY 'DIVISION NO. ' DA-DIVISION-NUMBER
053500D        DISPLAY 'DIVISION NAME ' DA-DIVISION-NAME.
053600
053700 GET-DIVISION-NAME.
053800D    DISPLAY 'GETTING DIVISION NAME FOR ' FN-DIVISION-CD.
054000     MOVE SUB-DIV TO DIVISION-CODE.
054700     MOVE '203' TO CT-DIVISION-SEQUENCE
054800     MOVE DIVISION-KEY TO DIVISION-I-O-AREA(1:10)
           MOVE DIVISION-CODE   TO XXXANCIAL-DIV-ID OF DCLXXXANCIAL-DIV
054900     SET YYYN005A-BATCH-ENV    TO  TRUE
055800D    DISPLAY 'WS-STC-SCTB0000 ' WS-STC-SCTB0000.

           EXEC SQL
             SELECT XXXANCIAL_DIV_NM
               INTO :DCLXXXANCIAL-DIV.XXXANCIAL-DIV-NM
             FROM XXXANCIAL_DIV
             WHERE XXXANCIAL_DIV_ID = :DCLXXXANCIAL-DIV.XXXANCIAL-DIV-ID
           END-EXEC

056000     MOVE SQLCODE TO DB2-SQL-CODE
056000     EVALUATE TRUE
056100     WHEN SUCCESS
056200       IF DB2-SQL-CODE = 0
               MOVE XXXANCIAL-DIV-NM OF DCLXXXANCIAL-DIV                
                 TO CT-DIVISION-NAME                                    
056300         MOVE CT-DIVISION-NAME TO DIVISION-NAME (SUB-DIV)
056600       ELSE IF DB2-SQL-CODE = 100
056700            MOVE 'Y' TO DIVISION-FLAG
056800            MOVE INVALID-DIV-MSG TO
056900               DIVISION-NAME (SUB-DIV)
057100         END-IF
057200         END-IF
057300     WHEN OTHER
057400         MOVE 'GET DIV ' TO DB2-ERROR-PARAGRAPH
052720         SET DB2121-PRINT-AND-DUMP TO TRUE
052730         CALL 'TTTS2001' USING SQLCA
052740                                 DB2121-MESSAGE-AREA.
057900     SKIP2
058000 CLOSE-FILES.
058100     CLOSE XXXP-AUDIT-STRIP
058200           XXXP-DAILY-AUDIT
058300           ERROR-FILE.
058400     EJECT
058500 GET-STORE-ERROR.
058600     MOVE 'GET STO ' TO DB2-ERROR-PARAGRAPH.
052720     SET DB2121-PRINT-AND-DUMP TO TRUE
052730     CALL 'TTTS2001' USING SQLCA
052740                                 DB2121-MESSAGE-AREA.
059000     SKIP2
059100 ERROR-PARA.
059200     IF FIRST-TIME
059300        MOVE 'Y' TO HEAD-FLAG
059400        WRITE ERROR-REC FROM ERROR-HEAD
059500        WRITE ERROR-REC FROM SPACED-OUT.
059600     IF STORE-ERROR
059700        MOVE 'STORE NUMBER NOT FOUND ON STORE DATA BASE' TO
059800          ERROR-MSG
059900        PERFORM WRITE-ERROR.
060000     IF DEPT-ERROR
060100        MOVE 'DEPT NUMBER NOT FOUND ON CONTROL DATA BASE' TO
060200          ERROR-MSG
060300        IF STORE-ERROR
060400           WRITE ERROR-REC FROM ERROR-OUT
060500        ELSE
060600           PERFORM WRITE-ERROR.
060700     IF DISTRICT-ERROR
060800        MOVE 'DISTRICT NUMBER NOT FOUND ON CONTROL DATA BASE' TO
060900          ERROR-MSG
061000        IF STORE-ERROR OR DEPT-ERROR
061100           WRITE ERROR-REC FROM ERROR-OUT
061200        ELSE
061300           PERFORM WRITE-ERROR.
061400     IF DIVISION-ERROR
061500        MOVE 'DIVISION NUMBER NOT FOUND ON CONTROL DATA BASE' TO
061600          ERROR-MSG
061700        IF STORE-ERROR OR DEPT-ERROR OR DISTRICT-ERROR
061800           WRITE ERROR-REC FROM ERROR-OUT
061900        ELSE
062000           PERFORM WRITE-ERROR.
062100     WRITE ERROR-REC FROM SPACED-OUT.
062200     MOVE SPACES TO ERROR-FLAGS.
062300     EJECT
062400 WRITE-ERROR.
062500         MOVE DA-COMPANY-NUMBER TO E-COMPANY
062600         MOVE DA-MAJOR-ACCT TO E-ACCT
062700         MOVE DA-MINOR-ACCT TO E-MIN
062800         MOVE DA-STORE-NUMBER TO E-STORE
062900         MOVE DA-DEPT-SUB-DEPT TO E-DEPT
063000         MOVE DA-MAJOR-ACCOUNT-TYPE TO E-MAJ-TYPE
063100         MOVE DA-ACCOUNT-TYPE TO E-ACCT-TYPE
063200         MOVE DA-DISTRICT-NUMBER TO E-DIST-DIV
063300         MOVE DA-COMMON-DATE TO E-DATE
063400         MOVE DA-COMMON-TIME TO E-TIME
063500         MOVE DA-DIVISION-NUMBER TO E-DIST-DIV
063600     WRITE ERROR-REC FROM ERROR-OUT.
