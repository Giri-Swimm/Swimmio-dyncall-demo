000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.    TTTP2002.
000300 AUTHOR.        NAME.
000400 DATE-WRITTEN.  1600/08/30
000500 DATE-COMPILED.
003400*****************************************************************
003500 ENVIRONMENT DIVISION.
003600 CONFIGURATION SECTION.
003700*
003800*SOURCE-COMPUTER.    HITACHI-H120    WITH DEBUGGING MODE.
003900*
004000*********************************************************************
004100* FILE INFORMATION                                                  *
004200*********************************************************************
004300 INPUT-OUTPUT SECTION.
004400 FILE-CONTROL.
004500     SELECT ERROR-OUTPUT-FILE ASSIGN TO DB2ERROR
004600        FILE STATUS IS WS-FILE-STATUS.
004700
004800 DATA DIVISION.
004900 FILE SECTION.
005000 FD  ERROR-OUTPUT-FILE
005100     BLOCK CONTAINS 0 RECORDS
005200     LABEL RECORDS ARE STANDARD.
005300 01  ERROR-RECORD                 PIC X(80).
005400
005500*********************************************************************
005600* WORKING STORAGE SECTION                                           *
005700*********************************************************************
005800 WORKING-STORAGE SECTION.
005900
006000 77 PROGNAME           PIC X(8)  VALUE 'TTTP2002'.
006100 77 FILLER             PIC X(28)
006200                       VALUE '-WORKING STORAGE STARTS HERE'.
006300 77 WS-DISP-SQLCODE-3  PIC +999.
006400 77 WS-DISP-SQLCODE-4  PIC +9999.
006500 77 WS-DISP-SQLCODE-5  PIC +99999.
006600 77 WS-DISP-RC         PIC ZZZZZZZZ9-.
006700
006800
006900 01 WS-FILE-STATUS     PIC XX.
007000    88 GOOD-FILE-STATUS      VALUE '00'.
007100    88 DB2ERROR-DD-MISSING   VALUE '96'.
007200    88 DB2ERROR-NOT-OPEN     VALUE '42'.
007300
007400
007500 01 WS-FIRST-LINE.
007600    05 FILLER             PIC X(14)
007700                          VALUE 'DB2-ERROR-ID: '.
007800    05 WS-ERROR-ID        PIC X(66).
007900
008000
008100 01 WS-FLAGS.
008200    10 WS-FILE-PROBLEMS      PIC X.
008300       88 NO-FILE-PROBLEMS         VALUE '0'.
008400       88 FILE-PROBLEMS            VALUE '1'.
008500    10 WS-PRINT-CHECK        PIC X.
008600       88 BLANK-LINE-NOT-FOUND     VALUE '0'.
008700       88 BLANK-LINE-FOUND         VALUE '1'.
008800    10 WS-REASON-CODE-FLAG   PIC X.
008900       88 REASON-CODE-NOT-FOUND    VALUE '0'.
009000       88 REASON-CODE-FOUND        VALUE '1'.
009100    10 WS-QWIKREF-PROBLEMS   PIC X.
009200       88 NO-QWIKREF-PROBLEMS      VALUE '0'.
009300       88 QWIKREF-PROBLEMS         VALUE '1'.
009400    10 WS-VALID-SQLCODE      PIC X.
009500       88 INVALID-SQLCODE          VALUE '0'.
009600       88 VALID-SQLCODE            VALUE '1'.
009700    10 WS-VALID-PARM         PIC X.
009800       88 INVALID-PARM             VALUE '0'.
009900       88 VALID-PARM               VALUE '1'.
010000    10 WS-DSNTIAR-PROBLEMS   PIC X.
010100       88 NO-DSNTIAR-PROBLEMS      VALUE '0'.
010200       88 DSNTIAR-PROBLEMS         VALUE '1'.
010300    10 WS-PRINT-SWITCH       PIC X.
010400       88 NO-PRINT-REQUESTED       VALUE '0'.
010500       88 PRINT-REQUESTED          VALUE '1'.
010600    10 WS-ABEND-SWITCH       PIC X.
010700       88 NO-ABEND-REQUESTED       VALUE '0'.
010800       88 ABEND-REQUESTED          VALUE '1'.
010900    10 WS-DUMP-SWITCH        PIC X.
011000       88 NO-DUMP-REQUESTED        VALUE '0'.
011100       88 DUMP-REQUESTED           VALUE '1'.
011200
011300
011400 01 WS-REASON-CODE-VARS.
011500    05 WS-REASON-CODE-LEN        PIC 9(4) COMP.
011600    05 WS-REASON-CODE            PIC X(8).
011700    05 WS-REASON-CODE-SPLIT REDEFINES WS-REASON-CODE.
011800       10 WS-REASON-CODE-PREFIX  PIC X(3).
011900          88 VALID-RSN-CD-PREFIX VALUES '00C' '00D' '00E' '00F'.
012000       10 FILLER                 PIC X(5).
012100
012200
012300 01 WS-JOB-CONTROL-BLOCK-INFO.
012400    05 WS-STEP-PROGNAME   PIC X(8).
012500       88 IMS-PROGRAM     VALUES 'DFSRRC00'
012600                                 'BMPSHELL'
012700                                 'BMPSHEL3'
012800                                 'BMPSHEL4'
012900                                 'BMPSHEL5'
013000                                 'BMPSHEL6'.
013100    05 WS-JOB-PREFIX      PIC X(3).
013200       88 BATCH-MODE      VALUE 'JOB'.
013300       88 ONLINE-MODE     VALUE 'STC'.
013400    05 WS-IMS-DRIVER      PIC X(8).
013500
013600
013700 01 WS-CALC-VARS.
013800    05 WS-NUM-LINES       PIC 9(9) COMP.
013900    05 WS-TRUNC-INCREMENT PIC 9(9) COMP.
014000    05 WS-LINE-NO         PIC 9(9) COMP.
014100    05 WS-MESSAGE-INDEX   PIC S9(4) COMP.
014200    05 WS-SQLERRMC-PTR    PIC S9(4) COMP.
014300
014400
014500 01 WS-CEE-ABEND-OPTIONS.
014600    05 WS-ABCODE        PIC S9(9) COMP VALUE 4032.
014700    05 WS-ABTYPE        PIC S9(9) COMP VALUE 1.
014800       88 NO-CEE-DUMP   VALUE 0.
014900       88 CEE-DUMP      VALUE 1.
015000
015100
015200 01 WS-MESSAGE-LINE-BLOCK.
015300    05 WS-MESSAGE-LINE       PIC X(80).
015400    05 WS-MESSAGE-LINE-BYTE
015500       REDEFINES WS-MESSAGE-LINE
015600                             PIC X OCCURS 80
015700                             INDEXED BY WS-LINE-BYTE-PTR.
015800
015900
016000 01 WS-LITERALS.
016100    05 WS-DSNTIAR            PIC X(8)  VALUE 'DSNTIAR'.
016200    05 WS-QWIKREF1           PIC X(8)  VALUE 'QWIKREF1'.
016300    05 WS-ERROR-DIVIDER.
016400       10 FILLER             PIC X(40)
016500          VALUE '========================================'.
016600       10 FILLER             PIC X(40)
016700          VALUE '========================================'.
016800
016900
017000*** QWIKREF COPYBOOKS
017100********************************************* 2003/06/09 - PGLB - COMNT
017200*   COPY QWIKDPC1.
017300*   COPY QWIKDPC2.
017400********************************************* 2003/06/09 - PGLB - END
017500
017600
017700*********************************************************************
017800* LINKAGE SECTION                                                   *
017900*********************************************************************
018000 LINKAGE SECTION.
018100
018200     EXEC SQL
018300        INCLUDE SQLCA
018400     END-EXEC.
018500
018600
018700    COPY T01N2001.
018800
018900
019000    01 DSNTIAR-MESSAGE-PTR         PIC X.
019100    01 DSNTIAR-LRECL-PTR           PIC X.
019200
019300
019400********************************************* 2003/06/09 - PGLB - COMNT
019500*** QWIKREF COPYBOOK
019600*   COPY QCPY.
019700    01 DUMMY-PARM1                 PIC X.
019800    01 DUMMY-PARM2                 PIC X.
019900********************************************* 2003/06/09 - PGLB - COMNT
020000
020100
020200*** MVS CONTROL BLOCKS
020300    01 PSA-CONTROL-BLOCK.
020400       05 FILLER                   PIC X(540).
020500       05 TCB-PTR                  POINTER.
020600    01 TCB-CONTROL-BLOCK.
020700       05 RB-PTR                   POINTER.
020800       05 FILLER                   PIC X(176).
020900       05 JSCB-PTR                 POINTER.
021000    01 JSCB-CONTROL-BLOCK.
021100       05 FILLER                   PIC X(316).
021200       05 SSIB-PTR                 POINTER.
021300       05 FILLER                   PIC X(40).
021400       05 STEP-PROGNAME            PIC X(8).
021500    01 SSIB-CONTROL-BLOCK.
021600       05 FILLER                   PIC X(12).
021700       05 JOB-TYPE                 PIC X(3).
021800    01 RB-CONTROL-BLOCK.
021900       05 FILLER                   PIC X(96).
022000       05 LINK-PROGNAME            PIC X(8).
022100       05 RB-OTHER                 PIC X(40).
022200
022300*********************************************************************
022400* PROCEDURE DIVISION                                                *
022500*********************************************************************
022600 PROCEDURE DIVISION    USING   SQLCA
022700                               DB2-MESSAGE-AREA
022800********************************************* 2003/06/09 - PGLB - COMNT
022900* REPLACED QWIK-REF PARMS WITH DUMMY PARMS
023000*                              QW-LOOKUP-MSG
023100*                              QW-RESULT-MSG.
023200                               DUMMY-PARM1
023300                               DUMMY-PARM2.
023400********************************************* 2003/06/09 - PGLB - COMNT
023500
023600
023700
023800     PERFORM 100-INITIALIZE
023900
024000     PERFORM 150-VALIDATE-OPTION-PARM
024100
024200     IF PRINT-REQUESTED
024300        PERFORM 200-WRITE-IDENTIFIER
024400
024500     PERFORM 300-GET-DB2-MESSAGE
024600
024700     IF PRINT-REQUESTED AND NO-DSNTIAR-PROBLEMS
024800        PERFORM 400-PRINT-MESSAGE
024900           VARYING WS-LINE-NO FROM 1 BY 1
025000           UNTIL BLANK-LINE-FOUND
025100********************************************* 2003/06/09 - PGLB - COMNT
025200*       IF BATCH-MODE AND NO-FILE-PROBLEMS
025300*          PERFORM 700-PRINT-QWIKREF-INFO
025400*       END-IF
025500********************************************* 2003/06/09 - PGLB - END
025600     END-IF
025700
025800     PERFORM 500-CLEANUP
025900
026000     GOBACK
026100     .
026200
026300 100-INITIALIZE.
026400
026500     SET NO-FILE-PROBLEMS TO TRUE
026600     SET BLANK-LINE-NOT-FOUND TO TRUE
026700     SET REASON-CODE-NOT-FOUND TO TRUE
026800     SET NO-QWIKREF-PROBLEMS TO TRUE
026900     SET NO-DSNTIAR-PROBLEMS TO TRUE
027000     SET VALID-SQLCODE TO TRUE
027100     MOVE 1 TO WS-MESSAGE-INDEX
027200     MOVE 1 TO WS-SQLERRMC-PTR
027300*    MOVE SPACES TO QW-RESULT-MSG
027400     PERFORM 600-OPEN-ERROR-FILE
027500     PERFORM 900-GET-PROGRAM-INFO
027600     IF DB2-ERROR-PROGNAME NOT EQUAL WS-IMS-DRIVER
027700     AND DB2-ERROR-PARAGRAPH EQUAL SPACES
027800        MOVE DB2-ERROR-PROGNAME TO DB2-ERROR-PARAGRAPH
027900        MOVE WS-IMS-DRIVER TO DB2-ERROR-PROGNAME
028000     END-IF
028100     .
028200
028300 150-VALIDATE-OPTION-PARM.
028400
028500     EVALUATE TRUE
028600
028700        WHEN DB2-PRINT-DUMP-ABEND
028800           SET PRINT-REQUESTED TO TRUE
028900           SET DUMP-REQUESTED TO TRUE
029000           SET ABEND-REQUESTED TO TRUE
029100
029200        WHEN DB2-PRINT-DUMP-RETURN
029300           SET PRINT-REQUESTED TO TRUE
029400           SET DUMP-REQUESTED TO TRUE
029500           SET NO-ABEND-REQUESTED TO TRUE
029600
029700        WHEN DB2-PRINT-RETURN
029800           SET PRINT-REQUESTED TO TRUE
029900           SET NO-DUMP-REQUESTED TO TRUE
030000           SET NO-ABEND-REQUESTED TO TRUE
030100
030200        WHEN DB2-RETURN
030300           SET NO-PRINT-REQUESTED TO TRUE
030400           SET NO-DUMP-REQUESTED TO TRUE
030500           SET NO-ABEND-REQUESTED TO TRUE
030600
030700        WHEN OTHER
030800           SET INVALID-PARM TO TRUE
030900           SET PRINT-REQUESTED TO TRUE
031000           SET DUMP-REQUESTED TO TRUE
031100           SET ABEND-REQUESTED TO TRUE
031200           STRING 'TTTP2002- INVALID DB2 OPTION-BYTE PASSED:'
031300                                            DELIMITED BY SIZE
031400                  DB2-OPTION-BYTE DELIMITED BY SIZE
031500             INTO WS-MESSAGE-LINE
031600           DISPLAY WS-MESSAGE-LINE UPON CONSOLE
031700           PERFORM 610-WRITE-TO-DB2ERROR-DD
031800
031900     END-EVALUATE
032000     .
032100
032200 200-WRITE-IDENTIFIER.
032300
032400     IF DB2-ERROR-IDENTIFIER EQUAL TO SPACES
032500        IF IMS-PROGRAM
032600           STRING 'DB2 ERROR TRAPPED '         DELIMITED BY SIZE
032700                  'IN ENCLAVE '                DELIMITED BY SIZE
032800                  WS-IMS-DRIVER                DELIMITED BY SIZE
032900             INTO WS-ERROR-ID
033000        ELSE
033100           STRING 'DB2 ERROR TRAPPED '         DELIMITED BY SIZE
033200                  'IN ENCLAVE '                DELIMITED BY SIZE
033300                  WS-STEP-PROGNAME             DELIMITED BY SIZE
033400             INTO WS-ERROR-ID
033500        END-IF
033600     ELSE
033700        MOVE DB2-ERROR-IDENTIFIER TO WS-ERROR-ID
033800     END-IF
033900     MOVE WS-FIRST-LINE TO WS-MESSAGE-LINE
034000     PERFORM 610-WRITE-TO-DB2ERROR-DD
034100     PERFORM 610-WRITE-TO-DB2ERROR-DD
034200     IF DB2-MESSAGE-LINE(1) NOT EQUAL SPACES
034300        MOVE DB2-MESSAGE-LINE(1) TO WS-MESSAGE-LINE
034400        PERFORM 610-WRITE-TO-DB2ERROR-DD
034500        PERFORM 610-WRITE-TO-DB2ERROR-DD
034600     END-IF
034700     .
034800
034900 300-GET-DB2-MESSAGE.
035000
035100     SET ADDRESS OF DSNTIAR-MESSAGE-PTR
035200         TO ADDRESS OF DB2-MESSAGE-LENGTH
035300     SET ADDRESS OF DSNTIAR-LRECL-PTR
035400         TO ADDRESS OF DB2-LINE-LENGTH
035500
035600     CALL WS-DSNTIAR  USING  SQLCA
035700                             DSNTIAR-MESSAGE-PTR
035800                             DSNTIAR-LRECL-PTR
035900************ MADE COBOL II COMPLIANT FOR TELON - 2003/06/23 - PGLB
036000******* ON EXCEPTION
036100*******    SET DSNTIAR-PROBLEMS TO TRUE
036200*******    MOVE 'TTTP2002- EXCEPTION LOADING DSNTIAR MODULE!!'
036300*******      TO WS-MESSAGE-LINE
036400*******    DISPLAY WS-MESSAGE-LINE UPON CONSOLE
036500*******    PERFORM 610-WRITE-TO-DB2ERROR-DD
036600******* NOT ON EXCEPTION
036700           CANCEL WS-DSNTIAR
036800**** END-CALL
036900************ MADE COBOL II COMPLIANT FOR TELON - 2003/06/23 - PGLB
037000
037100     IF NO-DSNTIAR-PROBLEMS
037200        EVALUATE RETURN-CODE
037300           WHEN 0
037400              CONTINUE
037500           WHEN 4
037600              MOVE 'TTTP2002- DSNTIAR - MESSAGE AREA OVERFLOW'
037700                TO WS-MESSAGE-LINE
037800              DISPLAY WS-MESSAGE-LINE UPON CONSOLE
037900              PERFORM 610-WRITE-TO-DB2ERROR-DD
038000           WHEN 8
038100              MOVE 'TTTP2002- DSNTIAR - LRECL OUT OF RANGE'
038200                TO WS-MESSAGE-LINE
038300              DISPLAY WS-MESSAGE-LINE UPON CONSOLE
038400              PERFORM 610-WRITE-TO-DB2ERROR-DD
038500           WHEN 12
038600              MOVE 'TTTP2002- DSNTIAR - TEXT AREA TOO SMALL'
038700                TO WS-MESSAGE-LINE
038800              DISPLAY WS-MESSAGE-LINE UPON CONSOLE
038900              PERFORM 610-WRITE-TO-DB2ERROR-DD
039000           WHEN 16
039100              MOVE 'TTTP2002- INTERNAL ERROR IN DSNTIAR ROUTINE!'
039200                TO WS-MESSAGE-LINE
039300              DISPLAY WS-MESSAGE-LINE UPON CONSOLE
039400              PERFORM 610-WRITE-TO-DB2ERROR-DD
039500           WHEN 20
039600              MOVE 'TTTP2002- DSNTIAR COULD NOT LOAD DSNTIA1!!'
039700                TO WS-MESSAGE-LINE
039800              DISPLAY WS-MESSAGE-LINE UPON CONSOLE
039900              PERFORM 610-WRITE-TO-DB2ERROR-DD
040000           WHEN 24
040100              MOVE 'TTTP2002- INVALID SQLCA PASSED TO DSNTIAR!!'
040200                TO WS-MESSAGE-LINE
040300              DISPLAY WS-MESSAGE-LINE UPON CONSOLE
040400              PERFORM 610-WRITE-TO-DB2ERROR-DD
040500              MOVE 'SQLCA:' TO WS-MESSAGE-LINE
040600              PERFORM 610-WRITE-TO-DB2ERROR-DD
040700              MOVE SQLCA TO WS-MESSAGE-LINE
040800              PERFORM 610-WRITE-TO-DB2ERROR-DD
040900           WHEN OTHER
041000              MOVE RETURN-CODE TO WS-DISP-RC
041100              STRING 'TTTP2002- BAD RETURN CODE FROM DSNTIAR: '
041200                                              DELIMITED BY SIZE
041300                     WS-DISP-RC               DELIMITED BY SIZE
041400                INTO WS-MESSAGE-LINE
041500              DISPLAY WS-MESSAGE-LINE UPON CONSOLE
041600              PERFORM 610-WRITE-TO-DB2ERROR-DD
041700        END-EVALUATE
041800        IF RETURN-CODE > 4
041900           SET DSNTIAR-PROBLEMS TO TRUE
042000           SET INVALID-PARM TO TRUE
042100           SET PRINT-REQUESTED TO TRUE
042200           SET DUMP-REQUESTED TO TRUE
042300           SET ABEND-REQUESTED TO TRUE
042400        END-IF
042500     END-IF
042600     .
042700
042800 400-PRINT-MESSAGE.
042900
043000     IF DB2-MESSAGE-LINE(WS-LINE-NO) = SPACES
043100        THEN SET BLANK-LINE-FOUND TO TRUE
043200        ELSE
043300           MOVE DB2-MESSAGE-LINE(WS-LINE-NO)
043400              TO WS-MESSAGE-LINE
043500           PERFORM 610-WRITE-TO-DB2ERROR-DD
043600           IF WS-LINE-NO = 10
043700              THEN SET BLANK-LINE-FOUND TO TRUE
043800           END-IF
043900     END-IF
044000     .
044100
044200 500-CLEANUP.
044300
044400     PERFORM 620-CLOSE-ERROR-FILE
044500
044600     EVALUATE TRUE
044700        WHEN INVALID-SQLCODE
044800           MOVE 24 TO RETURN-CODE
044900        WHEN DSNTIAR-PROBLEMS
045000           MOVE 20 TO RETURN-CODE
045100        WHEN FILE-PROBLEMS
045200           MOVE 16 TO RETURN-CODE
045300        WHEN INVALID-PARM
045400           MOVE 12 TO RETURN-CODE
045500        WHEN ABEND-REQUESTED
045600           MOVE 8  TO RETURN-CODE
045700        WHEN DUMP-REQUESTED
045800           MOVE 4  TO RETURN-CODE
045900        WHEN OTHER
046000           MOVE 0  TO RETURN-CODE
046100     END-EVALUATE
046200     .
046300
046400 600-OPEN-ERROR-FILE.
046500
046600     MOVE SPACES TO WS-MESSAGE-LINE
046700
046800     OPEN OUTPUT ERROR-OUTPUT-FILE
046900
047000     EVALUATE TRUE
047100       WHEN GOOD-FILE-STATUS
047200          CONTINUE
047300       WHEN DB2ERROR-DD-MISSING
047400          DISPLAY 'TTTP2002- DB2ERROR DD CARD MISSING'
047500             UPON CONSOLE
047600          DISPLAY 'TTTP2002- ROUTING OUTPUT TO CONSOLE'
047700             UPON CONSOLE
047800          SET FILE-PROBLEMS TO TRUE
047900       WHEN OTHER
048000          DISPLAY 'TTTP2002- BAD FILE STATUS ON DB2ERROR OPEN: '
048100             WS-FILE-STATUS UPON CONSOLE
048200          DISPLAY 'TTTP2002- ROUTING OUTPUT TO CONSOLE'
048300             UPON CONSOLE
048400          SET FILE-PROBLEMS TO TRUE
048500     END-EVALUATE
048600     .
048700
048800 610-WRITE-TO-DB2ERROR-DD.
048900
049000     IF NO-FILE-PROBLEMS
049100        WRITE ERROR-RECORD FROM WS-MESSAGE-LINE
049200        IF NOT GOOD-FILE-STATUS
049300           SET FILE-PROBLEMS TO TRUE
049400           DISPLAY 'TTTP2002- BAD STATUS WRITING TO DB2ERROR: '
049500              WS-FILE-STATUS UPON CONSOLE
049600           DISPLAY 'TTTP2002- ROUTING OUTPUT TO CONSOLE'
049700              UPON CONSOLE
049800           DISPLAY 'TTTP2002- ' WS-MESSAGE-LINE
049900              UPON CONSOLE
050000        END-IF
050100     ELSE
050200        DISPLAY 'TTTP2002- ' WS-MESSAGE-LINE
050300           UPON CONSOLE
050400     END-IF
050500
050600     MOVE SPACES TO WS-MESSAGE-LINE
050700     .
050800
050900 620-CLOSE-ERROR-FILE.
051000
051100     IF ONLINE-MODE
051200        MOVE WS-ERROR-DIVIDER TO WS-MESSAGE-LINE
051300        PERFORM 610-WRITE-TO-DB2ERROR-DD
051400     END-IF
051500
051600     CLOSE ERROR-OUTPUT-FILE
051700
051800     EVALUATE TRUE
051900       WHEN GOOD-FILE-STATUS
052000          CONTINUE
052100       WHEN DB2ERROR-NOT-OPEN
052200        AND FILE-PROBLEMS
052300          CONTINUE
052400       WHEN OTHER
052500          DISPLAY 'TTTP2002- BAD FILE STATUS ON DB2ERROR CLOSE: '
052600             WS-FILE-STATUS UPON CONSOLE
052700          SET FILE-PROBLEMS TO TRUE
052800     END-EVALUATE
052900     .
053000
053100********************************************* 2003/06/09 - PGLB - COMNT
053200*700-PRINT-QWIKREF-INFO.
053300*
053400*    PERFORM 610-WRITE-TO-DB2ERROR-DD
053500*    MOVE '000'       TO QW-LOOKUP-ITEM-ID
053600*    MOVE 'IBM'       TO QW-LOOKUP-VENDOR-ID
053700*    MOVE 'DB2*'      TO QW-LOOKUP-PRODUCT-ID
053800*    MOVE 'V6*'       TO QW-LOOKUP-RELEASE-ID
053900*    MOVE SPACES      TO QW-RESULT-MSG
054000*
054100*    PERFORM 800-GET-REASON-CODE
054200*
054300*    IF REASON-CODE-FOUND
054400*       PERFORM 705-CALL-QWIKREF
054500*       IF RETURN-CODE = 8 AND NO-QWIKREF-PROBLEMS
054600*          PERFORM 820-FORMAT-SQLCODE
054700*          IF VALID-SQLCODE
054800*             MOVE SPACES TO QW-RESULT-MSG
054900*             PERFORM 705-CALL-QWIKREF
055000*          END-IF
055100*       END-IF
055200*    ELSE
055300*       PERFORM 820-FORMAT-SQLCODE
055400*       IF VALID-SQLCODE
055500*          PERFORM 705-CALL-QWIKREF
055600*       END-IF
055700*    END-IF
055800*
055900*    IF QWIKREF-PROBLEMS OR RETURN-CODE > 8
056000*       PERFORM 720-QWIKREF-ERROR
056100*    ELSE
056200*       PERFORM 710-WRITE-QWIKREF-INFO
056300*    END-IF
056400*    .
056500*
056600*705-CALL-QWIKREF.
056700*
056800*    MOVE LENGTH OF QW-LOOKUP-MSG TO QW-LOOKUP-LENGTH
056900*    MOVE LENGTH OF QW-RESULT-MSG TO QW-OUTPUT-MAX-LENGTH
057000*    SET QW-INPUT-ADDRESS TO ADDRESS OF QW-LOOKUP-MSG
057100*    SET QW-OUTPUT-ADDRESS TO ADDRESS OF QW-RESULT-MSG
057200*
057300*    CALL WS-QWIKREF1 USING QWIKDPC1
057400*                           QWIKDPC2
057500*       ON EXCEPTION
057600*          SET QWIKREF-PROBLEMS TO TRUE
057700*          PERFORM 610-WRITE-TO-DB2ERROR-DD
057800*          MOVE 'TTTP2002- EXCEPTION LOADING QWIKREF1 MODULE!!'
057900*            TO WS-MESSAGE-LINE
058000*          DISPLAY WS-MESSAGE-LINE UPON CONSOLE
058100*          PERFORM 610-WRITE-TO-DB2ERROR-DD
058200*       NOT ON EXCEPTION
058300*          CANCEL WS-QWIKREF1
058400*    END-CALL
058500*    .
058600*
058700*710-WRITE-QWIKREF-INFO.
058800*
058900*    COMPUTE WS-NUM-LINES =
059000*       QW-OUTPUT-MAX-LENGTH / QW-OUTPUT-LRECL - 1
059100*
059200*    IF QW-OUTPUT-LRECL GREATER THAN 80
059300*       THEN COMPUTE WS-TRUNC-INCREMENT = QW-OUTPUT-LRECL - 80
059400*       ELSE MOVE ZERO TO WS-TRUNC-INCREMENT
059500*    END-IF
059600*
059700*    SET QW-MSG-BYTE-PTR TO 1
059800*
059900*    PERFORM VARYING WS-LINE-NO FROM 1 BY 1
060000*       UNTIL WS-LINE-NO GREATER THAN WS-NUM-LINES
060100*
060200*       PERFORM VARYING WS-LINE-BYTE-PTR FROM 1 BY 1
060300*          UNTIL WS-LINE-BYTE-PTR GREATER THAN 80
060400*             OR WS-LINE-BYTE-PTR GREATER THAN QW-OUTPUT-LRECL
060500*
060600*          MOVE QW-RESULT-MSG-BYTE(QW-MSG-BYTE-PTR)
060700*             TO WS-MESSAGE-LINE-BYTE(WS-LINE-BYTE-PTR)
060800*
060900*          SET QW-MSG-BYTE-PTR UP BY 1
061000*
061100*       END-PERFORM
061200*
061300*       SET QW-MSG-BYTE-PTR UP BY WS-TRUNC-INCREMENT
061400*
061500*       PERFORM 610-WRITE-TO-DB2ERROR-DD
061600*
061700*    END-PERFORM
061800*    .
061900*
062000*720-QWIKREF-ERROR.
062100*
062200*    IF RETURN-CODE > 0
062300*       MOVE RETURN-CODE TO WS-DISP-RC
062400*       STRING ' - BAD RETURN CODE FROM '   DELIMITED BY SIZE
062500*              'QWIKREF1: '                 DELIMITED BY SIZE
062600*              WS-DISP-RC                   DELIMITED BY SIZE
062700*          INTO WS-MESSAGE-LINE
062800*    ELSE
062900*       MOVE 'PROBLEM CALLING QWIKREF1' TO WS-MESSAGE-LINE
063000*    END-IF
063100*
063200*    PERFORM 610-WRITE-TO-DB2ERROR-DD
063300*
063400*    STRING 'QWIKREF PARMS: '            DELIMITED BY SIZE
063500*           QW-LOOKUP-MSG-TXT            DELIMITED BY SIZE
063600*           INTO WS-MESSAGE-LINE
063700*    PERFORM 610-WRITE-TO-DB2ERROR-DD
063800*    .
063900*
064000*800-GET-REASON-CODE.
064100*
064200*    IF SQLERRML > 0
064300*       PERFORM 810-SCAN-FOR-REASON-CODE
064400*          UNTIL REASON-CODE-FOUND
064500*             OR WS-SQLERRMC-PTR >= SQLERRML
064600*    END-IF
064700*    .
064800*
064900*810-SCAN-FOR-REASON-CODE.
065000*
065100*    UNSTRING SQLERRMC
065200*       DELIMITED BY X'FF' OR ALL SPACES
065300*       INTO         WS-REASON-CODE
065400*       COUNT IN     WS-REASON-CODE-LEN
065500*       WITH POINTER WS-SQLERRMC-PTR
065600*    END-UNSTRING
065700*
065800*    IF WS-REASON-CODE-LEN = 8 AND VALID-RSN-CD-PREFIX
065900*       MOVE WS-REASON-CODE TO QW-LOOKUP-ITEM-ID
066000*       SET REASON-CODE-FOUND TO TRUE
066100*    END-IF
066200*    .
066300*
066400*820-FORMAT-SQLCODE.
066500*    MOVE 'SQL*'      TO QW-LOOKUP-PRODUCT-ID
066600*    EVALUATE SQLCODE
066700*       WHEN +000
066800*          CONTINUE
066900*       WHEN -999   THRU +999
067000*          MOVE SQLCODE TO WS-DISP-SQLCODE-3
067100*          MOVE WS-DISP-SQLCODE-3 TO QW-LOOKUP-ITEM-ID
067200*       WHEN -9999  THRU +9999
067300*          MOVE SQLCODE TO WS-DISP-SQLCODE-4
067400*          MOVE WS-DISP-SQLCODE-4 TO QW-LOOKUP-ITEM-ID
067500*       WHEN -99999 THRU +99999
067600*          MOVE SQLCODE TO WS-DISP-SQLCODE-5
067700*          MOVE WS-DISP-SQLCODE-5 TO QW-LOOKUP-ITEM-ID
067800*       WHEN OTHER
067900*          DISPLAY 'TTTP2002- SQLCODE OUT OF RANGE'
068000*             UPON CONSOLE
068100*          DISPLAY WS-MESSAGE-LINE UPON CONSOLE
068200*          PERFORM 610-WRITE-TO-DB2ERROR-DD
068300*          SET INVALID-SQLCODE TO TRUE
068400*    END-EVALUATE
068500*    .
068600********************************************* 2003/06/09 - PGLB - END
068700
068800**********************************************************************
068900* CONTROL BLOCK LAYOUTS CAN BE FOUND IN THE "MVS DATA AREAS" MANUALS *
069000**********************************************************************
069100 900-GET-PROGRAM-INFO.
069200
069300*    PSA (PREFIXED SAVE AREA) STARTS AT ADDRESS ZERO
069400     SET ADDRESS OF PSA-CONTROL-BLOCK   TO  NULL
069500
069600*    PSA  OFFSET 540 POINTS TO TCB (TASK CONTROL BLOCK)
069700     SET ADDRESS OF TCB-CONTROL-BLOCK   TO  TCB-PTR
069800
069900*    TCB  OFFSET 0   POINTS TO RB (REQUEST BLOCK)
070000     SET ADDRESS OF RB-CONTROL-BLOCK    TO  RB-PTR
070100
070200*    TCB  OFFSET 180 POINTS TO JSCB (JOB/STEP CONTROL BLOCK)
070300     SET ADDRESS OF JSCB-CONTROL-BLOCK  TO  JSCB-PTR
070400
070500*    JSCB OFFSET 316 POINTS TO SSIB (SUBSYSTEM IDENTIFICATION BLOCK)
070600     SET ADDRESS OF SSIB-CONTROL-BLOCK  TO  SSIB-PTR
070700
070800*    JSCB OFFSET 360 CONTAINS JOB STEP PROGRAM NAME
070900     MOVE STEP-PROGNAME TO WS-STEP-PROGNAME
071000
071100*    SSIB OFFSET 12  CONTAINS JOB/STC ID
071200     MOVE JOB-TYPE TO WS-JOB-PREFIX
071300
071400*    RB   OFFSET 96  CONTAINS EXTENDED SAVE AREA INFO, OF WHICH
071500*                    THE FIRST 8 BYTES IS THE FIRST PROGRAM
071600*                    CALLED USING MVS "LINK" TYPE CALL
071700     MOVE LINK-PROGNAME TO WS-IMS-DRIVER
071800
071900     IF NOT IMS-PROGRAM
072000        MOVE 'TTTP2002- WARNING: NOT EXECUTING UNDER IMS CONTROL!'
072100             TO WS-MESSAGE-LINE
072200        DISPLAY WS-MESSAGE-LINE
072300                 UPON CONSOLE
072400        PERFORM 610-WRITE-TO-DB2ERROR-DD
072500        STRING 'TTTP2002- EXECUTING PROGRAM: ' DELIMITED BY SIZE
072600               WS-STEP-PROGNAME                DELIMITED BY SIZE
072700               INTO WS-MESSAGE-LINE
072800        DISPLAY WS-MESSAGE-LINE
072900                 UPON CONSOLE
073000        PERFORM 610-WRITE-TO-DB2ERROR-DD
073100     END-IF
073200     .
