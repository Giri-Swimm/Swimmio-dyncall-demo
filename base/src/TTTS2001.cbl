000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.    TTTS2001.
000300 AUTHOR.        NAME.
000400 DATE-WRITTEN.  1600/08/30
000500 DATE-COMPILED.
004300*****************************************************************
004400 ENVIRONMENT DIVISION.
004500 CONFIGURATION SECTION.
004600*
004700*SOURCE-COMPUTER.    HITACHI-H120    WITH DEBUGGING MODE.
004800*
004900 DATA DIVISION.
005000 WORKING-STORAGE SECTION.
005100
005200   77 TTTP2002                     PIC X(8) VALUE 'TTTP2002'.
005300   77 WS-DISP-RC                   PIC ZZZZZZZZ9-.
005400   77 SSSABEND                     PIC X(8) VALUE 'SSSABEND'.
005500   77 WS-SQLCODE-DISP              PIC +ZZ9.
005600
005700   01 WS-ACTION-CODE               PIC S9(9) COMP VALUE 0.
005800      88 RETURN-CONTROL-TO-CALLER  VALUE 0.
005900      88 DUMP-N-ABEND              VALUE 4 8.
006000
006100   01 SSSABEND-PARMS.
006200      02 SSSABEND-CODE  PIC X(4) VALUE '4031'.
006300      02 SSSABEND-MESG  PIC X(80)
006400         VALUE 'DB2 ERROR ABEND FROM TTTS2001'.
006500
006600 LINKAGE SECTION.
006700
006800     EXEC SQL INCLUDE SQLCA END-EXEC.
006900
007000   COPY TTTN2001.
007100
007200
007300 PROCEDURE DIVISION      USING  SQLCA
007400                                DB279-MESSAGE-AREA.
007500
007600     PERFORM 100-DIAGNOSE-N-PRINT
007700
007800     EVALUATE TRUE
007900
008000        WHEN  RETURN-CONTROL-TO-CALLER
008100              CONTINUE
008200
008300        WHEN  DUMP-N-ABEND
008400              MOVE SQLCODE TO WS-SQLCODE-DISP
008500              STRING
008600                 'ABEND FROM '
008700                 DB2-ERROR-PROGNAME       DELIMITED BY SIZE
008800                 ' - DB2 ERROR SQLCODE: ' DELIMITED BY SIZE
008900                 WS-SQLCODE-DISP          DELIMITED BY SIZE
009000                 INTO SSSABEND-MESG
009100              PERFORM 999-SSSABEND-DUMP-N-ABEND
009200
009300        WHEN  OTHER
009400              MOVE 4032 TO SSSABEND-CODE
009500              MOVE RETURN-CODE TO WS-DISP-RC
009600              DISPLAY 'TTTS2001: BAD RETURN CODE FROM TTTP2002:'
009700                 WS-DISP-RC UPON CONSOLE
009800              MOVE 'TTTS2001: BAD RETURN CODE FROM TTTP2002'
009900                 TO SSSABEND-MESG
010000              PERFORM 999-SSSABEND-DUMP-N-ABEND
010100
010200     END-EVALUATE
010300
010400     GOBACK
010500     .
010600********************************************************************
010700* TTTP2002 IS CALLED DYNAMICALLY - THIS PROGRAM PRINTS THE         *
010800* DIAGNOSTIC INFORMATION TO THE DB2ERROR DD                        *
010900********************************************************************
011000 100-DIAGNOSE-N-PRINT.
011100     CALL TTTP2002      USING  SQLCA
011200                               DB279-MESSAGE-AREA
011300     CANCEL TTTP2002
011400     MOVE RETURN-CODE TO WS-ACTION-CODE
011500     .
011600********************************************************************
011700* PERFORM A DUMP AND ABEND                                         *
011800********************************************************************
011900 999-SSSABEND-DUMP-N-ABEND.
012000     CALL SSSABEND USING SSSABEND-PARMS
012100     .
